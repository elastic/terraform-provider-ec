---
page_title: "Elastic Cloud: ec_deployment_extension Resource"
description: |-
  
  Provides an Elastic Cloud extension resource, which allows extensions to be created, updated, and deleted.

  Extensions allow users of Elastic Cloud to use custom plugins, scripts, or dictionaries to enhance the core functionality of Elasticsearch. Before you install an extension, be sure to check out the supported and official [Elasticsearch plugins](https://www.elastic.co/guide/en/elasticsearch/plugins/current/index.html) already available.

  **Tip :** If you experience timeouts when uploading an extension through a slow network, you might need to increase the [timeout setting](https://registry.terraform.io/providers/elastic/ec/latest/docs#timeout).

---

# Resource: ec_deployment_extension


  Provides an Elastic Cloud extension resource, which allows extensions to be created, updated, and deleted.

  Extensions allow users of Elastic Cloud to use custom plugins, scripts, or dictionaries to enhance the core functionality of Elasticsearch. Before you install an extension, be sure to check out the supported and official [Elasticsearch plugins](https://www.elastic.co/guide/en/elasticsearch/plugins/current/index.html) already available.

  **Tip :** If you experience timeouts when uploading an extension through a slow network, you might need to increase the [timeout setting](https://registry.terraform.io/providers/elastic/ec/latest/docs#timeout).


## Example Usage

### With extension file (not usable for ECE)

```terraform
locals {
  file_path = "/path/to/plugin.zip"
}

resource "ec_deployment_extension" "example_extension" {
  name           = "my_extension"
  description    = "my extension"
  version        = "*"
  extension_type = "bundle"

  file_path = local.file_path
  file_hash = filebase64sha256(local.file_path)
}

data "ec_stack" "latest" {
  version_regex = "latest"
  region        = "us-east-1"
}

resource "ec_deployment" "with_extension" {
  # Optional name.
  name = "my_example_deployment"

  # Mandatory fields
  region                 = "us-east-1"
  version                = data.ec_stack.latest.version
  deployment_template_id = "aws-io-optimized-v2"

  elasticsearch = {
    hot = {
      autoscaling = {}
    }
    extension = [{
      name    = ec_deployment_extension.example_extension.name
      type    = "bundle"
      version = data.ec_stack.latest.version
      url     = ec_deployment_extension.example_extension.url
    }]
  }
}
```

### With download URL

```terraform
resource "ec_deployment_extension" "example_extension" {
  name           = "my_extension"
  description    = "my extension"
  version        = "*"
  extension_type = "bundle"
  download_url   = "https://example.net"
}

data "ec_stack" "latest" {
  version_regex = "latest"
  region        = "us-east-1"
}

resource "ec_deployment" "with_extension" {
  # Optional name.
  name = "my_example_deployment"

  # Mandatory fields
  region                 = "us-east-1"
  version                = data.ec_stack.latest.version
  deployment_template_id = "aws-io-optimized-v2"

  elasticsearch = {
    hot = {
      autoscaling = {}
    }
    extension = [{
      name    = ec_deployment_extension.example_extension.name
      type    = "bundle"
      version = data.ec_stack.latest.version
      url     = ec_deployment_extension.example_extension.url
    }]
  }
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Required

- `extension_type` (String) Extension type. Must be `bundle` or `plugin`. A `bundle` will usually contain a dictionary or script, where a `plugin` is compiled from source.
- `name` (String) Name of the extension
- `version` (String) Elastic stack version. A full version (e.g 8.7.0) should be set for plugins. A wildcard (e.g 8.*) may be used for bundles.

### Optional

- `description` (String) Description for the extension
- `download_url` (String) The URL to download the extension archive.
- `file_hash` (String) Hash value of the file. Triggers re-uploading the file on change.
- `file_path` (String) Local file path to upload as the extension.

### Read-Only

- `id` (String) Unique identifier of this resource.
- `last_modified` (String) The datatime the extension was last modified.
- `size` (Number) The size of the extension file in bytes.
- `url` (String) The extension URL which will be used in the Elastic Cloud deployment plan.

## Import

Extensions can be imported using the `id`, for example:

```shell
terraform import ec_deployment_extension.name 320b7b540dfc967a7a649c18e2fce4ed
```
